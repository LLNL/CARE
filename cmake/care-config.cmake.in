##############################################################################
# Copyright (c) 2020-25, Lawrence Livermore National Security, LLC and CARE
# project contributors. See the CARE LICENSE file for details.
#
# SPDX-License-Identifier: BSD-3-Clause
##############################################################################

##########################################################################
# Set up CMake
##########################################################################

cmake_minimum_required(VERSION 3.23 FATAL_ERROR)

##########################################################################
# Set up package
##########################################################################

@PACKAGE_INIT@

##########################################################################
# Set up dependencies
##########################################################################

include(CMakeFindDependencyMacro)
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

set(CARE_UMPIRE_DIR            "@UMPIRE_DIR@")
set(CARE_RAJA_DIR              "@RAJA_DIR@")
set(CARE_CHAI_DIR              "@CHAI_DIR@")
set(CARE_LLNL_GLOBALID_DIR     "@LLNL_GLOBALID_DIR@")
set(CARE_CUDA_TOOLKIT_ROOT_DIR "@CUDA_TOOLKIT_ROOT_DIR@")
set(CARE_CUB_DIR               "@CUB_DIR@")

# Set to real variable unless user overrode it
foreach(dep UMPIRE RAJA CHAI LLNL_GLOBALID CUDA_TOOLKIT_ROOT CUB)
   if(NOT ${dep}_DIR)
      set(${dep}_DIR "${CARE_${dep}_DIR}")
   endif()
endforeach()

include(CMakeFindDependencyMacro)

include("${CMAKE_CURRENT_LIST_DIR}/BLTSetupTargets.cmake")

find_dependency(umpire NO_DEFAULT_PATH PATHS "${UMPIRE_DIR}")
find_dependency(raja NO_DEFAULT_PATH PATHS "${RAJA_DIR}")
find_dependency(chai NO_DEFAULT_PATH PATHS "${CHAI_DIR}")

if(@LLNL_GLOBALID_FOUND@)
   find_dependency(llnl_globalid NO_DEFAULT_PATH PATHS "${LLNL_GLOBALID_DIR}")
endif()

if(@CUB_FOUND@)
   find_dependency(CUB)
endif()

if(@CUDATOOLKIT_FOUND@)
   set(CUDAToolkit_ROOT ${CUDA_TOOLKIT_ROOT_DIR})
   find_dependency(CUDAToolkit)
endif()

##########################################################################
# Set up targets
##########################################################################

include("${CMAKE_CURRENT_LIST_DIR}/care-targets.cmake")

##########################################################################
# Check required components
##########################################################################

check_required_components(care)
